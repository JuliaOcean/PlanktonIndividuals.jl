mutable struct Model_Input
    temp::AbstractArray{Float64,4}      # temperature
    PARF::AbstractArray{Float64,3}      # PARF
end

mutable struct timestepper
    Gcs::NamedTuple     # a NamedTuple same as nutrients to store tendencies
    MD1::NamedTuple     # a NamedTuple same as nutrients to store nutrients fields in multi-dims advection scheme
    MD2::NamedTuple     # a NamedTuple same as nutrients to store nutrients fields in multi-dims advection scheme
    MD3::NamedTuple     # a NamedTuple same as nutrients to store nutrients fields in multi-dims advection scheme
    vel₀::NamedTuple    # a NamedTuple with u, v, w velocities
    vel½::NamedTuple    # a NamedTuple with u, v, w velocities
    vel₁::NamedTuple    # a NamedTuple with u, v, w velocities
    plk::NamedTuple     # a NamedTuple same as nutrients to store interactions with individuals
    par::AbstractArray  # a (Cu)Array to store PAR field of each timestep
    chl::AbstractArray  # a (Cu)Array to store Chl field of each timestep
    pop::AbstractArray  # a (Cu)Array to store population field of each timestep
    cts::AbstractArray  # a (Cu)Array to store counts of each timestep
    tmp::AbstractArray  # a (Cu)Array to store temporary individuals of each timestep
    rnd::AbstractArray  # a StructArray of random numbers for plankton diffusion or grazing, mortality and division.
    velos::AbstractArray# a StructArray of intermediate values for RK4 particle advection
    coord::AbstractArray# a StructArray of coordinates of each individual
end

mutable struct Model_Struct
    arch::Architecture          # architecture on which models will run
    t::Int64
    individuals::individuals    # initial individuals generated by `setup_agents`
    nutrients::NamedTuple       # initial nutrient fields
    grid::Grids                 # grid information
    input::Model_Input          # model input, temp and PAR
    params::Dict                # biogeochemical parameter set
    diags::Diagnostics          # diagnostics
    timestepper::timestepper    # operating Tuples and arrays for timestep
end

function timestepper(arch::Architecture, g::Grids, N)
    vel₀ = (u = Field(arch, g), v = Field(arch, g), w = Field(arch, g))
    vel½ = (u = Field(arch, g), v = Field(arch, g), w = Field(arch, g))
    vel₁ = (u = Field(arch, g), v = Field(arch, g), w = Field(arch, g))

    Gcs = nutrients_init(arch, g)
    MD1 = nutrients_init(arch, g)
    MD2 = nutrients_init(arch, g)
    MD3 = nutrients_init(arch, g)
    plk = nutrients_init(arch, g)

    par = zeros(g.Nx, g.Ny, g.Nz) |> array_type(arch)
    chl = zeros(g.Nx, g.Ny, g.Nz) |> array_type(arch)
    pop = zeros(g.Nx, g.Ny, g.Nz) |> array_type(arch)
    cts = zeros(g.Nx, g.Ny, g.Nz, 4N, 10) |> array_type(arch)
    tmp = zeros(4N,60) |> array_type(arch)

    rnd = StructArray(x = zeros(4N), y = zeros(4N), z = zeros(4N))
    rnd_d = replace_storage(array_type(arch), rnd)

    velos = StructArray(x = zeros(4N), y = zeros(4N), z = zeros(4N),
                        u⁺= zeros(4N), v⁺= zeros(4N), w⁺= zeros(4N),
                        u⁻= zeros(4N), v⁻= zeros(4N), w⁻= zeros(4N),
                        u1= zeros(4N), v1= zeros(4N), w1= zeros(4N),
                        u2= zeros(4N), v2= zeros(4N), w2= zeros(4N),
                        u3= zeros(4N), v3= zeros(4N), w3= zeros(4N),
                        u4= zeros(4N), v4= zeros(4N), w4= zeros(4N),
                        xd= zeros(4N), yd= zeros(4N), zd= zeros(4N),
                        )
    velos_d = replace_storage(array_type(arch), velos)

    coord = StructArray(x = zeros(4N), y = zeros(4N), z = zeros(4N))
    coord_d = replace_storage(array_type(arch), coord)

    ts = timestepper(Gcs, MD1, MD2, MD3, vel₀, vel½, vel₁, plk, par, chl, pop, cts, tmp, rnd_d, velos_d, coord_d)

    return ts
end

"""
    PI_model(grid, RunParam)
Generate the model structure for time step
Default distribution of individuals is Normal distribution with 1.0 as mean and 0.25 as SD
Default PAR and temp are from ../samples
"""
function PI_Model(arch::Architecture, grid, RunParam;
                  t = 1-RunParam.ΔT,
                  individuals = individuals(RunParam.params, arch),
                  nutrients,
                  PARF = read_IR_input(RunParam.ΔT, grid),
                  temp = read_temp_input(RunParam.ΔT, grid),
                  params = RunParam.params,
                  diags = diags_setup(arch, RunParam.nTime, RunParam.ΔT, grid,
                                     RunParam.params["diag_freq"], RunParam.params["Nsp"], 5),
                  )

    if arch == GPUs() && !has_cuda()
        throw(ArgumentError("Cannot create a GPU model. No CUDA-enabled GPU was detected!"))
    end

    if arch == GPUs()
        input = Model_Input(CuArray(temp), CuArray(PARF))
    else
        input = Model_Input(temp,PARF)
    end

    for plank in individuals.phytos
        gen_individuals!(plank, params["Nind"], grid, arch)
    end

    ts = timestepper(arch, grid, params["Nind"])

    model = Model_Struct(arch, t, individuals, nutrients, grid, input, params, diags, ts)

    if RunParam.Zoo == true
        model.params["grz_P"] = 0
    else
        nothing
    end
    return model
end
